plugins {
	id 'com.android.library'
	id 'kotlin-android'
	id 'maven-publish'
}

android {
	namespace 'de.markusfisch.android.zxingcpp'
	compileSdk sdk_version

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion sdk_version

		externalNativeBuild {
			cmake {
				arguments "-DCMAKE_BUILD_TYPE=RelWithDebInfo", \
					"-DANDROID_ARM_NEON=ON"
			}
		}

		consumerProguardFiles 'consumer-rules.pro'
	}

	externalNativeBuild {
		cmake {
			path file('src/main/cpp/CMakeLists.txt')
		}
	}

	compileOptions {
		// Required for Gradle 8.
		sourceCompatibility = 17
		targetCompatibility = 17
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

publishing {
	publications {
		aar(MavenPublication) {
			artifactId = 'zxingcpp'
			groupId = project.group.toString()
			version = project.version.toString()

			artifact("$buildDir/outputs/aar/${project.name}-release.aar")

			pom.withXml {
				def dependenciesNode = asNode().appendNode('dependencies')
				configurations.implementation.allDependencies.each { dependency ->
					def dependencyNode = dependenciesNode.appendNode('dependency')
					dependencyNode.appendNode('groupId', dependency.group)
					dependencyNode.appendNode('artifactId', dependency.name)
					dependencyNode.appendNode('version', dependency.version)
				}
			}
		}
	}
}
